apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "io.grpc.helloworldexample"
        // API level 14+ is required for TLS since Google Play Services v10.2
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        debug { minifyEnabled false }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'GoogleAppIndexingWarning', 'HardcodedText', 'InvalidPackage'
        textReport true
        textOutput "stdout"
    }
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.12.3' }
    plugins {
        grpc { artifact = 'io.grpc:protoc-gen-grpc-java:1.30.2'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.addIncludeDir(files("../fix-javalite"))
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                grpc { // Options added to --grpc_out
                    option 'lite' }
            }
        }
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'io.grpc:grpc-okhttp:1.30.2'
    implementation 'io.grpc:grpc-protobuf-lite:1.30.2'
    implementation 'io.grpc:grpc-stub:1.30.2'
    implementation 'org.apache.tomcat:annotations-api:6.0.53'
}
